grammar de.tfritsch.psdt.Postscript hidden(WS, SL_COMMENT)
// there is no point to use  with org.eclipse.xtext.common.Terminals
// because we would need to override most of it (ID, STRING, SL_COMMENT, WS)
generate postscript "http://www.tfritsch.de/psdt/Postscript"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

PSFile:
	{PSFile}
	objects+=PSObject*;

PSObject:
	PSLiteralName | PSExecutableName | PSNumber | PSString | PSProcedure | PSArray | PSDictionary;

PSProcedure:
	{PSProcedure}
	'{' objects+=PSObject* '}';

PSArray:
	{PSArray}
	'[' objects+=PSObject* ']';

PSDictionary:
	{PSDictionary}
	'<<' objects+=PSObject* '>>';

PSLiteralName:
	name=LITERAL_ID;

PSExecutableName:
	name=ID;

PSNumber:
	n=NUMBER;

PSString:
	s=STRING | s=ASCII_HEX_STRING | s=ASCII_85_STRING;

terminal STRING:
	'(' (!('\\' | '(' | ')') | ESCAPED_CHAR)* ')';

terminal fragment ESCAPED_CHAR:
	'\\' ('t' | 'n' | 'f' | 'r' | 'b' | '(' | ')' | '\\' | ('0'..'7')+);

terminal ASCII_HEX_STRING:
	'<' ('0'..'9' | 'a'..'f' | 'A'..'F' | WS)* '>';

terminal ASCII_85_STRING:
	'<~' ('!'..'u' | 'z' | WS)* '~>';

terminal NUMBER:
	('-')? ('0'..'9') ('.' | '0'..'9')*;

terminal LITERAL_ID:
	'/' ID;

terminal ID:
	(!('0'..'9' | DELIM_CHAR)) (!DELIM_CHAR)*;

terminal fragment DELIM_CHAR:
	'(' | ')' | '<' | '>' | '[' | ']' | '{' | '}' | '/' | '%' | ' ' | '\t' | '\r' | '\n' | '\f' | '\u0000';

terminal SL_COMMENT:
	'%' !('\r' | '\n' | '\f')*;

terminal WS:
	(' ' | '\t' | '\r' | '\n' | '\f' | '\u0000')+;
