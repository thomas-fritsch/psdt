grammar de.tfritsch.psdt.Postscript hidden(WS, SL_COMMENT, DSC_COMMENT)
// there is no point to use  with org.eclipse.xtext.common.Terminals
// because we would need to override most of it (ID, STRING, SL_COMMENT, WS)
generate postscript "http://www.tfritsch.de/psdt/Postscript"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

PSFile:
	{PSFile}
	objects+=PSObject*;

PSObject:
	PSLiteralName | PSExecutableName | PSInt | PSFloat | PSString | PSProcedure | PSArray | PSDictionary |
	PSUnparsedData;

PSProcedure:
	{PSProcedure}
	'{' objects+=PSObject* '}';

PSArray:
	{PSArray}
	'[' objects+=PSObject* ']';

PSDictionary:
	{PSDictionary}
	'<<' objects+=PSObject* '>>';

PSLiteralName:
	name=LITERAL_ID;

PSExecutableName:
	name=ID;

PSUnparsedData:
	data=UNPARSED_DATA;

PSInt:
	i=INT;

PSFloat:
	f=FLOAT;

PSString:
	bytes=STRING | bytes=ASCII_HEX_STRING | bytes=ASCII_85_STRING;

terminal STRING returns ecore::EByteArray:
	'(' (!('\\' | '(' | ')') | ESCAPED_CHAR)* ')';

terminal fragment ESCAPED_CHAR:
	'\\' ('t' | 'n' | 'f' | 'r' | 'b' | '(' | ')' | '\\' | ('0'..'7')+);

terminal ASCII_HEX_STRING returns ecore::EByteArray:
	'<' ('0'..'9' | 'a'..'f' | 'A'..'F' | WS)* '>';

terminal ASCII_85_STRING returns ecore::EByteArray:
	'<~' ('!'..'u' | 'z' | WS)* '~>';

terminal INT returns ecore::EInt:
	(('+' | '-')? ('0'..'9')+) | RADIX_INT;

terminal fragment RADIX_INT:
	('0'..'9')+ '#' ('0'..'9' | 'a'..'z' | 'A'..'Z')+;

terminal FLOAT returns ecore::EFloat:
	('+' | '-')? ('0'..'9')+ '.' ('0'..'9')* EXP? |
	('+' | '-')? '.' ('0'..'9')+ EXP? |
	('+' | '-')? ('0'..'9')+ EXP;

terminal fragment EXP:
	('E' | 'e') ('+' | '-')? ('0'..'9')+;

terminal LITERAL_ID:
	'/' ID;

terminal ID:
	(!('0'..'9' | DELIM_CHAR)) (!DELIM_CHAR)*;

terminal fragment DELIM_CHAR:
	'(' | ')' | '<' | '>' | '[' | ']' | '{' | '}' | '/' | '%' | ' ' | '\t' | '\r' | '\n' | '\f' | '\u0000';

terminal UNPARSED_DATA:
	DSC_BEGIN_DATA .* DSC_END_DATA;

terminal fragment DSC_BEGIN_DATA:
	'%%BeginData:' !('\r' | '\n' | '\f')*;

terminal fragment DSC_END_DATA:
	'%%EndData' !('\r' | '\n' | '\f')*;

terminal DSC_COMMENT:
	('%!PS' | '%%') !('\r' | '\n' | '\f')*;

terminal SL_COMMENT:
	'%' !('\r' | '\n' | '\f')*;

terminal WS:
	(' ' | '\t' | '\r' | '\n' | '\f' | '\u0000')+;
